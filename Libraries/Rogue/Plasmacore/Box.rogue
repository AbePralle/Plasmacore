uses Geometry
uses Plasmacore

augment Box
  METHODS
    method at( anchor:Anchor )->XY
      return position + anchor.position * size

    method positioned_within( enclosing:Box, anchor=Anchor.CENTER:Anchor )->Box
      return Box( enclosing.position + anchor.position * (enclosing.size-size), size )

    method relative_to( anchor:Anchor )->Box
      # Returns the result box where Box(box.at(anchor),this.size) == this
      # E.g. if anchor is Anchor.BOTTOM and Box is [@100,100 20x20], the return value is
      # [@90,80 20x20]].
      return Box( position - anchor.position * size, size )

    method subset( subset_size:XY, anchor=Anchor.CENTER:Anchor )->Box
      return Box( position + anchor.position * (size-subset_size), subset_size )


$if not target("Console")
    method fill( color:Color )->this
      Display.fill( this, color )
      return this

    method fill( colors:Colors )->this
      Display.fill( this, colors )
      return this

    method draw( color:Color, thickness=1:Int32 )->this
      Display.draw( this, color, thickness )
      return this

    method transform( m:Matrix )->Quad
      return Quad(
        m * top_left,
        m * top_right,
        m * bottom_right,
        m * bottom_left
      )

    method transformed( m:Matrix )->Box
      return transform( m )->Box
$endIf
endAugment
