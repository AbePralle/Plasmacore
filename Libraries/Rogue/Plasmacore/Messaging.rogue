#==============================================================================
# Messaging.rogue
#
# Communication mechanism between Plasmacore and Native Layer.
#
# post()ing a message adds it to a queue that is sent en mass during the next
# update or before a send().
#
# send()ing a message pushes the message queue and then sends the current
# message directly. It allows for a message in response.
#
# TODO: make the following 'immediate' on the native side:
# Display.on_render
# Application.on_save
# Application.on_stop
#
# High-low byte order for Int32, Int64, and Real64
#
# Message Queue
#   while (has_another)
#     message_size : Int32              # number of bytes that follow not including this
#     message      : Byte[message_size]
#   endWhile
#
# Message
#   type_name_count : Int32                 # 0 means a reply
#   type_name_utf8  : UTF8[type_name_count]
#   message_id      : Int32                 # serial number (always present, only needed with RSVP replies)
#   immediate       : Byte                  # dispatch immediately instead of queuing?
#   timestamp       : Real64
#   while (position < message_size)
#     arg_name_count : Int32
#     arg_name       : UTF8[arg_name_count]
#     arg_data_type  : Byte
#     arg_data_size  : Int32
#     arg_data       : Byte[arg_data_size]
#   endWhile
#
# Data Types
#   DATA_TYPE_REAL64 = 1   # value:Int64 (Real64.integer_bits stored)
#   DATA_TYPE_INT64  = 2   # high:Int32, low:Int32
#   DATA_TYPE_INT32  = 3   # value:Int32
#   DATA_TYPE_BYTE   = 4   # value:Byte
#
#==============================================================================

module Plasmacore

$essential Byte[].clear()
$essential Byte[].reserve(Int32)

routine add_message_listener( type:String, fn:Function(Message), &once )->Function(Message)
  MessageManager.add_listener( type, fn, once )
  return fn
endRoutine

routine set_message_listener( type:String, fn:Function(Message), &once )->Function(Message)
  MessageManager.set_listener( type, fn, once )
  return fn
endRoutine

routine remove_message_listener( type:String, fn:Function(Message) )
  MessageManager.remove_listener( type, fn )
endRoutine

routine remove_message_listeners( type:String )
  MessageManager.remove_listeners( type )
endRoutine

class MessageManager [essential singleton]
  PROPERTIES
    io_buffer = Byte[]  # native layer accesses this directly
    io_reader = BufferedDataReader( io_buffer )

    message_builder = BufferedDataWriter()
    io_writer       = BufferedDataWriter( io_buffer )

    dispatch_buffer = Byte[]  # native layer accesses this directly
    dispatch_reader = BufferedDataReader( dispatch_buffer )
    dispatch_writer = BufferedDataWriter( dispatch_buffer )

    next_id   = 1
    string_buffer = StringBuilder()

    message_type_lookup = StringTable<<String>>()
    # Maps StringBuilder to String

    incoming_message_pool = Message[]
      # Recycled messages

    incoming_message_queue = Message[]
    incoming_message_active_list = Message[]

    outgoing_message = Message()
      # Recycled for outgoing messages

    listeners             = Table<<String,Function(Message)[]>>()
    one_time_listeners    = Table<<String,Function(Message)[]>>()
    reply_callbacks_by_id = Table<<Int32,Function(Message)>>()

  METHODS
    method init_object
      add_listener( "",  # Reply listener
        function(m:Message)
          local callback = MessageManager.reply_callbacks_by_id.remove( m.message_id )
          if (callback) callback( m )
        endFunction
      )

    method add_listener( type:String, listener:Function(Message), &once, &replace_existing )
      local table = select{ once:one_time_listeners || listeners }
      local _list = table[ type ]
      if (not _list)
        _list = Function(Message)[]
        table[ type ] = _list
      endIf
      if (replace_existing) _list.clear
      _list.add( listener )

    method remove_listener( type:String, listener:Function(Message) )
      local _list = listeners[ type ]
      if (_list) _list.remove( listener )
      _list = one_time_listeners[ type ]
      if (_list) _list.remove( listener )

    method remove_listeners( type:String )
      local _list = listeners[ type ]
      if (_list) _list.clear
      _list = one_time_listeners[ type ]
      if (_list) _list.clear

    method set_listener( type:String, listener:Function(Message), &once )
      add_listener( type, listener, &=once, &replace_existing )

    method consolidated_type_name( name:String )->String
      local consolidated = message_type_lookup[ name ]
      if (consolidated) return consolidated
      message_type_lookup[ name ] = name
      return name

    method consolidated_type_name( buffer:StringBuilder )->String
      local consolidated = message_type_lookup[ buffer ]
      if (consolidated) return consolidated
      consolidated = buffer->String
      message_type_lookup[ consolidated ] = consolidated
      return consolidated

    method create_message( reader:BufferedDataReader )->Message
      if (incoming_message_pool.is_empty) incoming_message_pool.add( Message() )
      local incoming_message = incoming_message_pool.remove_last
      incoming_message.init( reader )
      return incoming_message

    method create_message( type:String, message_id=null:Int32? )->Message
      if (not message_id.exists)
        message_id = next_id
        ++next_id
      endIf

      outgoing_message.init( type, message_id.value )
      return outgoing_message

    method dispatch_incoming_messages
      incoming_message_active_list.clear.add( incoming_message_queue )
      incoming_message_queue.clear
      dispatch_incoming_message( forEach in incoming_message_active_list )

    method dispatch_incoming_message( m:Message )
      local _list = listeners[ m.type ]
      if (_list)
        forEach (listener in _list step -1) listener( m )
      endIf

      _list = one_time_listeners[ m.type ]
      if (_list)
        local n = _list.count
        forEach (listener in _list step -1) listener( m )
        _list.discard( 0, n )
      endIf

      incoming_message_pool.add( m )  # recycle

    method dispatch_message->Logical
      # Creates and dispatches a message using dispatch_buffer & reader
      # Returns "true" if dispatch_buffer is rewritten with reply, "false" for no reply
      # TODO
      return false

    method post( m:Message )
      message_builder.write_int32( m.data.count )
      message_builder.write( m.data )

    method post_rsvp( m:Message, callback:Function(Message) )
      post( m )
      reply_callbacks_by_id[ m.message_id ] = callback

    method update->Byte[] [essential]
      # 'io_reader' has already been set up to decode 'io_buffer', which
      # in turn has already been filled with incoming message bytes by the
      # native layer.
      io_reader.reset

      while (io_reader.has_another)
        local incoming_message = create_message( io_reader )
        incoming_message_queue.add( incoming_message )

        # Most messages should be queued so that they can be executed at the beginning
        # of a render, when the graphics context is active and images can be loaded etc.
        # Urgent messages are dispatched immediately.
        if (incoming_message.immediate) dispatch_incoming_messages

      endWhile

      Plasmacore.global_update

      # Finalize the outgoing message data.
      io_writer.clear

      # Write out all the buffered data
      io_writer.write( message_builder.buffer )
      message_builder.clear

      TaskManager.update

      return io_buffer
endClass

class Message
  ENUMERATE
    DATA_TYPE_REAL64 = 1
    DATA_TYPE_INT64  = 2
    DATA_TYPE_INT32  = 3
    DATA_TYPE_BYTE   = 4

  GLOBAL METHODS
    method create( name:String )->Message
      return MessageManager.create_message( name )

  PROPERTIES
    type       : String
    message_id : Int32
    immediate  : Logical
    timestamp  : Real64

    data       = Byte[]
    reader     = BufferedDataReader( data )
    writer     = BufferedDataWriter( data )

    # Using arrays instead of tables to reduce dynamic allocation overhead
    keys       = String[]
    offsets    = Int32[]

    sent       : Logical

  METHODS
    method init
      timestamp = System.time
      immediate = false

    method init( new_type:String, message_id, immediate=false )
      timestamp = System.time
      if (not sent and type) println "ERROR: unsent outgoing message of type '$'." (type)
      type = MessageManager.consolidated_type_name( new_type )
      sent = false
      writer.clear

      _write_string( type )
      writer.write_int32( message_id )
      writer.write_logical( immediate )
      writer.write_real64( timestamp )

    method init( source:DataReader )
      keys.clear
      offsets.clear
      local size = source.read_int32
      source.read( data.clear, size )
      reader.reset
      type = _read_type_name
      message_id = reader.read_int32
      immediate = reader.read_logical
      timestamp = reader.read_real64
      while (_index_another) noAction

    method contains( name:String )->Logical
      return keys.locate(name).exists

    method create_reply->Message
      return MessageManager.create_message( "", message_id )

    method get( name:String )->Value
      local i = keys.locate_last( name )
      if (not i.exists) return UndefinedValue

      reader.seek( offsets[i.value] )

      local arg_type = reader.read_byte

      which (arg_type)
        case DATA_TYPE_REAL64
          return Value( real64(name) )

        case DATA_TYPE_INT64
          return Value( int64(name) )

        case DATA_TYPE_INT32
          return Value( int32(name) )

        case DATA_TYPE_BYTE
          return Value( string(name) )

        others
          return UndefinedValue
      endWhich

    method string( name:String, default="":String )->String
      local i = keys.locate( name )
      if (not i.exists) return default
      return string( name, MessageManager.string_buffer.clear )->String

    method string( name:String, buffer:StringBuilder )->StringBuilder
      local i = keys.locate_last( name )
      if (not i.exists) return buffer

      reader.seek( offsets[i.value] )
      local arg_type = reader.read_byte
      local arg_size = reader.read_int32
      if (arg_size == 0) return buffer

      which (arg_type)
        case DATA_TYPE_REAL64
          buffer.print( reader.read_real64 )

        case DATA_TYPE_INT64
          buffer.print( reader.read_int64 )

        case DATA_TYPE_INT32
          buffer.print( reader.read_int32 )

        case DATA_TYPE_BYTE
          buffer.reserve( arg_size )
          forEach (1..arg_size) buffer.write( reader.read_byte )
      endWhich

      return buffer

    method real64( name:String, default=0:Real64 )->Real64
      local i = keys.locate_last( name )
      if (not i.exists) return default

      reader.seek( offsets[i.value] )
      local arg_type = reader.read_byte
      local arg_size = reader.read_int32
      if (arg_size == 0) return default

      which (arg_type)
        case DATA_TYPE_REAL64
          return reader.read_real64

        case DATA_TYPE_INT64
          return reader.read_int64

        case DATA_TYPE_INT32
          return reader.read_int32

        case DATA_TYPE_BYTE
          return reader.read_byte
      endWhich

      return default

    method int64( name:String, default=0:Int64 )->Int64
      local i = keys.locate_last( name )
      if (not i.exists) return default

      reader.seek( offsets[i.value] )
      local arg_type = reader.read_byte
      local arg_size = reader.read_int32
      if (arg_size == 0) return default

      which (arg_type)
        case DATA_TYPE_REAL64
          return reader.read_real64->Int64

        case DATA_TYPE_INT64
          return reader.read_int64

        case DATA_TYPE_INT32
          return reader.read_int32

        case DATA_TYPE_BYTE
          return reader.read_byte
      endWhich

      return default

    method int32( name:String, default=0:Int32 )->Int32
      local i = keys.locate_last( name )
      if (not i.exists) return default

      reader.seek( offsets[i.value] )
      local arg_type = reader.read_byte
      local arg_size = reader.read_int32
      if (arg_size == 0) return default

      which (arg_type)
        case DATA_TYPE_REAL64
          return reader.read_real64->Int32

        case DATA_TYPE_INT64
          return reader.read_int64->Int32

        case DATA_TYPE_INT32
          return reader.read_int32

        case DATA_TYPE_BYTE
          return reader.read_byte
      endWhich

      return default

    method logical( name:String, default=false:Logical )->Logical
      return int32( name, ?:{default:1||0} ) != 0

    method bytes( name:String, bytes=Byte[]:Byte[] )->Byte[]
      local i = keys.locate_last( name )
      if (not i.exists) return bytes

      reader.seek( offsets[i.value] )
      local arg_type = reader.read_byte
      local arg_size = reader.read_int32
      if (arg_size == 0) return bytes

      bytes.reserve( arg_size )
      forEach (1..arg_size) bytes.add( reader.read->Byte )

      return bytes

    method value( name:String, default=UndefinedValue:Value )->Value
      local json = string( name, null as String )
      if (not json) return default

      return JSON.parse( json )

    method list( name:String, default=UndefinedValue:Value )->Value
      local result = value( name )
      if (result.is_list) return result
      else                return default

    method table( name:String, default=UndefinedValue:Value )->Value
      local result = value( name )
      if (result.is_table) return result
      else                 return default

    method set( name:String, _value:String )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_BYTE )
      _write_string( _value )
      return this

    method set( name:String, _value:Real64 )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_REAL64 ).write_int32( 8 )
      writer.write_real64( _value )
      return this

    method set( name:String, _value:Int64 )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_INT64 ).write_int32( 8 )
      writer.write_int64( _value )
      return this

    method set( name:String, _value:Int32 )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_INT32 ).write_int32( 4 )
      writer.write_int32( _value )
      return this

    method set( name:String, _value:Logical )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_BYTE ).write_int32( 1 )
      writer.write_byte( _value->Int32 )
      return this

    method set( name:String, bytes:Byte[] )->this
      _write_string( name )
      writer.write_byte( Message.DATA_TYPE_BYTE ).write_int32( bytes.count )
      forEach (b in bytes) writer.write_byte( b )
      return this

    method set( name:String, _value:Value )->this
      return set( name, _value.to_json )

    method post
      MessageManager.post( this )
      sent = true

    method post_rsvp( callback:Function(Message) )
      # This form of post_rsvp waits for the native layer to reply() directly to this
      # message.
      MessageManager.post_rsvp( this, callback )
      sent = true

    method post_rsvp( response_message_type:String, callback:Function(Message) )
      # Rather than awaiting a reply to this specific message, this form of post_rsvp
      # installs a global one-time message listener that calls the given callback
      # when a new message (not a reply) of the given response type comes through.
      add_message_listener( response_message_type, callback, &once )
      MessageManager.post( this )
      sent = true

    # INTERNAL USE ------------------------------------------------------------

    method _index_another->Logical
      if (not reader.has_another) return false
      local key    = _read_type_name
      local offset = reader.position
      keys.add( key )
      offsets.add( offset )

      reader.read_byte                   # skip arg type
      reader.skip( reader.read_int32 )   # skip arg data

      return true

    method _read_string( builder:StringBuilder )->StringBuilder
      local n = reader.read_int32
      forEach (1..n) builder.write( reader.read_byte )
      return builder

    method _read_type_name->String
      return MessageManager.consolidated_type_name( _read_string(MessageManager.string_buffer.clear) )

    method _write_string( value:String )
      local n = value.byte_count
      writer.write_int32( n )
      forEach (i in 0..<n) writer.write_byte( value.byte(i) )

endClass

