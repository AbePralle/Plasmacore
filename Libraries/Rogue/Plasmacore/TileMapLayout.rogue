module Plasmacore

class TileMapLayout
  PROPERTIES
    # Settings
    clamp_edges       = true
    offscreen_padding = 1     # number of tiles beyond visible edges to include in draw

    # Properties expressed in world/tile coordinates
    tiles_wide        : Int32
    tiles_high        : Int32
    clip_i1           : Int32
    clip_j1           : Int32
    clip_i2           : Int32
    clip_j2           : Int32
    camera            : XY  # tile position of camera

    # Properties expressed in screen/pixel coordinates
    viewport          : Box
    origin            : XY      # screen position of camera
    scale             = 1.0
    tile_size         = XY(1,1) # in nominal pixels

    # Internal
    needs_update      = true
    scaled_tile_size  = XY(1,1)
    screen_bounds     : Box  # corners may be off-screen

  METHODS
    method init( tiles_wide, tiles_high, tile_size=XY(1,1) )
      needs_update = true
      viewport = Display.bounds
      clip_i1 = 0
      clip_j1 = 0
      clip_i2 = tiles_wide - 1
      clip_j2 = tiles_high - 1

    method init( tiles_wide:Int32, tiles_high:Int32, tile_size:Int32 )
      init( tiles_wide, tiles_high, XY(tile_size,tile_size) )

    method draw_column( column_i:Int32, callback:Function(index:Int32,ij:XY,bounds:Box) )
      draw_subset( column_i, 0, column_i, tiles_high-1, callback )

    method draw_row( row_j:Int32, callback:Function(index:Int32,ij:XY,bounds:Box) )
      draw_subset( 0, row_j, tiles_wide-1, row_j, callback )

    method draw_subset( i1:Int32, j1:Int32, i2:Int32, j2:Int32, callback:Function(index:Int32,ij:XY,bounds:Box) )
      local original_clip_i1 = clip_i1
      local original_clip_j1 = clip_j1
      local original_clip_i2 = clip_i2
      local original_clip_j2 = clip_j2
      clip_i1 = i1
      clip_j1 = j1
      clip_i2 = i2
      clip_j2 = j2
      draw( callback )
      clip_i1 = original_clip_i1
      clip_j1 = original_clip_j1
      clip_i2 = original_clip_i2
      clip_j2 = original_clip_j2

    method draw( callback:Function(index:Int32,ij:XY,bounds:Box) )
      update_bounds

      local ts = scaled_tile_size

      local x1 = screen_bounds.x - ts.x*offscreen_padding
      local y1 = screen_bounds.y - ts.y*offscreen_padding
      local i1 = -offscreen_padding
      local j1 = -offscreen_padding
      if (i1 < clip_i1)
        x1 += (clip_i1-i1) * ts.x
        i1 = clip_i1
      endIf
      if (j1 < clip_j1)
        y1 += (clip_j1-j1) * ts.y
        j1 = clip_j1
      endIf
      if (x1 < -ts.x)
        local diff = ((-ts.x - x1) / ts.x).floor->Int32
        i1 += diff
        x1 += diff * ts.x
      endIf
      if (y1 < -ts.y)
        local diff = ((-ts.y - y1) / ts.y).floor->Int32
        j1 += diff
        y1 += diff * ts.y
      endIf

      local map_bottom_right = screen_to_map( viewport.bottom_right ).floor + XY(offscreen_padding,offscreen_padding)
      local i2 = map_bottom_right.x->Int32.or_smaller( clip_i2 )
      local j2 = map_bottom_right.y->Int32.or_smaller( clip_j2 )

      block
        local y = y1
        local index_start = j1*tiles_wide + i1
        forEach (j in j1..j2)
          local x = x1
          local index = index_start
          forEach (i in i1..i2)
            local tl = XY(x,y)
            local br = (tl + ts).round
            tl = tl.round
            callback( index, XY(i,j), Box(tl,br-tl) )
            x += ts.x
            ++index
          endForEach
          y += ts.y
          index_start += tiles_wide
        endForEach
      endBlock

    method height->Real64
      return screen_bounds.size.y

    method ij_to_index( i:Int32, j:Int32 )->Int32
      return j * width + i

    method index_to_ij( index:Int32 )->XY
      return XY( index%tiles_wide, index/tiles_wide )

    method map_to_screen( xy:XY )->XY
      update_bounds
      return (xy * scaled_tile_size + screen_bounds.position).round

    method map_to_screen( box:Box )->Box
      update_bounds
      local tl = (box.position * scaled_tile_size + screen_bounds.position).round
      local br = ((box.position+box.size) * scaled_tile_size + screen_bounds.position).round
      return Box( tl, br-tl )

    method screen_to_map( xy:XY )->XY
      update_bounds
      return (xy - screen_bounds.position) / scaled_tile_size

    method set_camera( @camera )
      needs_update = true

    method set_clip( clip:Box? )
      if (clip.exists)
        clip_i1 = clip.value.position.x->Int32
        clip_j1 = clip.value.position.y->Int32
        clip_i2 = ((clip_i1 + clip.value.size.x) - 1)->Int32
        clip_j2 = ((clip_j1 + clip.value.size.y) - 1)->Int32
      else
        clip_i1 = 0
        clip_j1 = 0
        clip_i2 = tiles_wide - 1
        clip_j2 = tiles_high - 1
      endIf

    method set_scale( @scale )
      needs_update = true

    method set_origin( @origin )
      needs_update = true

    method set_viewport( viewport:Box )
      set_viewport( viewport, viewport.center )

    method set_viewport( @viewport, @origin )
      needs_update = true

    method size->XY
      return screen_bounds.size

    method tile_index_at( screen_pos:XY )->Int32?
      local ij = tile_ij_at( screen_pos )
      if (not ij.exists) return null
      return ij.value.y * tiles_wide + ij.value.x

    method tile_ij_at( screen_pos:XY )->XY?
      if (not screen_bounds.contains(screen_pos)) return null
      return screen_to_map( screen_pos ).floor

    method update_bounds
      if (not needs_update) return
      needs_update = false

      scaled_tile_size = tile_size * scale
      screen_bounds = Box( origin-camera*scaled_tile_size, XY(tiles_wide,tiles_high)*scaled_tile_size )
      local underflow = (viewport.size - screen_bounds.size)
      if (underflow.x > 0)
        if (clamp_edges)
          local new_pos = viewport.position + XY( underflow.x/2, screen_bounds.y )
          screen_bounds = Box( new_pos, screen_bounds.size )
        endIf
      else
        if (clamp_edges)
          if (screen_bounds.x > viewport.x)
            screen_bounds -= XY(screen_bounds.x - viewport.x,0)
          elseIf (screen_bounds.x2 < viewport.x2)
            screen_bounds += XY(viewport.x2 - screen_bounds.x2,0)
          endIf
        endIf
      endIf
      if (underflow.y > 0)
        if (clamp_edges)
          local new_pos = viewport.position + XY( screen_bounds.x, underflow.y/2 )
          screen_bounds = Box( new_pos, screen_bounds.size )
        endIf
      else
        if (clamp_edges)
          if (screen_bounds.y > viewport.y)
            screen_bounds -= XY(0,screen_bounds.y - viewport.y)
          elseIf (screen_bounds.y2 < viewport.y2)
            screen_bounds += XY(0,viewport.y2 - screen_bounds.y2)
          endIf
        endIf
      endIf

    method width->Real64
      return screen_bounds.size.x

endClass

